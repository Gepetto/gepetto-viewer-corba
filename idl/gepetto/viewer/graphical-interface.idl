// file graphicalinterface.idl
module gepetto {
  /// Corba exception travelling through the Corba channel
  exception Error
  {
    string msg;
  };

  /// Sequence of names
  typedef sequence <string> Names_t;

module corbaserver {

// Comments :
// 1)  Node names are parsed, so if you call a node "node1/xxx", this node will
//     be automatically added as a child of node1
// 2)  Functions return False if an error occured (in that case, check the
//     SceneViewer-corbaserver window for more information)
// 3)  applyConfiguration doesn't move object straigh forward, you need to used
//     the "refresh" function each time you want actualize position of the Viewer.
//     This system is used to synchronize movement of all objects.

typedef float Transform [7];
typedef float Position [3];
typedef float Color [4];
typedef unsigned long WindowID;

  /// Corba exception travelling through the Corba channel
  interface GraphicalInterface {

    /// Set time between window frames
    /// \param input rate : waiting time (ms) between two frames.
    boolean setRate(in long rate) raises (Error);

    /// Change configurations according to the last applyConfigurations.d
    void refresh() raises (Error);


    /// Create a new window.
    /// \param input name : name of the new window.
    /// return the ID of the newly create window.
    unsigned long createWindow(in string name) raises (Error);
    unsigned long getWindowID (in string name) raises (Error);


    /// Print names of nodes on the SceneViewer-corbaserver terminal.
    Names_t getNodeList() raises (Error);

    /// Print names of windows on the SceneViewer-corbaserver terminal.
    Names_t getWindowList() raises (Error);

    /// Create a group node.
    /// \param input sceneName : name of the group.
    void createScene(in string sceneName) raises (Error);

    /// Create a group node and a GroudNode ("sceneName/floor") as child
    /// \param input sceneName : name of the group
    void createSceneWithFloor(in string sceneName) raises (Error);

    /// Add groupNode sceneName to the WindowManager windowName so
    /// sceneName and all its children will be displayed in the window windowName.
    /// \param input sceneName : name of the groupNode.
    /// \param input windowId : ID of rhe window.
    boolean addSceneToWindow(in string sceneName, in unsigned long windowId) raises (Error);

    /// create a box called boxName.
    /// \param input boxName : name of the box.
    /// \param input boxSize1 : lenght of the box (x axis).
    /// \param input boxSize2 : lenght of the box (y axis).
    /// \param input boxSize3 : lenght of the box (z axis).
    /// \param input RGBAcolor : Color of the box.
    boolean addBox(in string boxName, in float boxSize1, in float boxSize2, in float boxSize3, in Color RGBAcolor) raises (Error);

    /// create a box called CapsuleName.
    /// \param input CapsuleName : name of the capsule.
    /// \param input radius : radius of the capsule.
    /// \param input height : lenght of the segment that generates the capsule.
    /// \param input RGBAcolor : Color of the capsule.
    boolean addCapsule(in string capsuleName, in float radius, in float height, in Color RGBAcolorid) raises (Error);

    boolean addRod(in string rodName, in Color RGBAcolorid, in float radius, in float totalLength,in short maxCapsule) raises (Error);
    
    /// resize the capsule CapsuleName.
    /// \param input CapsuleName : name of the capsule.
    /// \param input height : lenght of the segment that generates the capsule.
    boolean resizeCapsule(in string capsuleName,in float height) raises (Error);

    /// create a node to display a mesh (works for .dae, .obj and some other formats)
    /// \param input meshName : name of the new meshNode.
    /// \param input meshPath : full path to the mesh file.
    boolean addMesh(in string meshName, in string meshPath) raises (Error);

    /// create a node to display a mesh (works for .dae, .obj and some other formats)
    /// \param input meshName : name of the new meshNode.
    /// \param input meshPath : full path to the mesh file.
    boolean addCone(in string coneName, in float radius, in float height, in Color RGBAcolor) raises (Error);

    /// create a Cyllinder called cylinderName
    /// \param input cylinderName : name of the cylinder.
    /// \param input radius : radius of the cylinder.
    /// \param input height : lenght of the cylinder.
    /// \param input RGBAcolor : color of the cylinder.
    boolean addCylinder(in string cylinderName, in float radius, in float height, in Color RGBAcolor) raises (Error);

    /// create a sphere called sphereName
    /// \param input sphereName : name of the sphere.
    /// \param input radius : radius of the sphere.
    /// \param input RGBAcolor : color of the sphere.
    boolean addSphere(in string sphereName, in float radius, in Color RGBAcolor) raises (Error);

    /// create a line called lineName.
    /// \param input lineName : name of the line.
    /// \param input pos1 : position one extremity of the line.
    /// \param input pos2 : position the other extremity of the line.
    /// \param input RGBAcolor : color of the line.
    boolean addLine(in string lineName, in Position pos1, in Position pos2, in Color RGBAcolor) raises (Error);

    /// create a four vertices face called faceName.
    /// \param input faceName : name of the face.
    /// \param input pos1, pos2, pos3, pos4 : position of the vertices.
    /// \param input RGBAcolor : color of the face.
    boolean addSquareFace(in string faceName, in Position pos1, in Position pos2, in Position pos3, in Position pos4, in Color RGBAcolor) raises (Error);

    /// create a three vertices face called faceName.
    /// \param input faceName : name of the face.
    /// \param input pos1, pos2, pos3 : position of the vertices.
    /// \param input RGBAcolor : color of the face.
    boolean addTriangleFace(in string faceName, in Position pos1, in Position pos2, in Position pos3, in Color RGBAcolor) raises (Error);


		// Add a sphere with xyz axis
		// The colors are x=red, y=green, z=blue (xyz=RGB)
		// \param input nodeName : name of the node
		// \param input RGBAcolor : color of the sphere
		// \param input radius : radius of the sphere
		// \param input sizeAxis : size of the axis (proportionnaly to the sphere radius, size axis = 1 -> axis are radius*4 in length and radius/4 in radius)
		boolean addXYZaxis(in string nodeName, in Color RGBAcolor,in float radius,in float sizeAxis) raises(Error);

		// Add an empty roadmap to the scene
	  // \param input nameCorba : name of the roadmap
	  // \param input RGBAcolorNode : color of the sphere
	  // \param input radius : radius of the sphere
	  // \param input sizeAxis :size of the axis (proportionnaly to the sphere radius, size axis = 1 -> axis are radius*4 in length and radius/4 in radius)
	  // \param input RGBAcolorEdge : color of the edges
    boolean createRoadmap(in string nameCorba,in Color RGBAcolorNode, in float radius, in float sizeAxis, in Color RGBAcolorEdge) raises(Error);

		// Add an edge to the roadmap
	  // \param input nameRoadmap : name of the roadmap
	  // \param input posFrom : position of the beginning of the edge
	  // \param input posTo : position of the end of the edge (not oriented edge, order doesn't matter)
    boolean addEdgeToRoadmap(in string nameRoadmap, in Position posFrom, in Position posTo)raises(Error);

		// Add a node to the roadmap
	  // \param input nameRoadmap : name of the roadmap
	  // \param input configuration : configuration of the node 
    boolean addNodeToRoadmap(in string nameRoadmap, in Transform configuration)raises(Error);

    /// Create a node from an urdf file
    /// \param robotName Name of the node that will contain the robot geometry,
    ///                  each geometric part is prefixed by this name,
    /// \param urdfFilePath to the package containing the urdf file,
    ///                       i.e. "/opt/ros/hydro/share/pr2_description",
    /// \param meshDataRootDir path to the package that contains the collada
    ///                        files, this path should finish by "/",
    ///                        i.e. "/opt/ros/hydro/share/"
    /// \note the parser will replace "package://" by meshDataRootDir in the
    ///       urdf file.
    /// a groupNode is created and each link will be child node of this node.
    /// Links are named "prefix/linkName", linkName is the name of the link
    /// in the URDF file.

    boolean addURDF (in string robotName, in string urdfFilePath,
		     in string meshDataRootDir) raises (Error);

    /// Create a node from an urdf file
    /// \param robotName Name of the node that will contain the robot geometry,
    ///                  each geometric part is prefixed by this name,
    /// \param urdfFilePath to the package containing the urdf file,
    ///                       i.e. "/opt/ros/hydro/share/pr2_description",
    /// \param meshDataRootDir path to the package that contains the collada
    ///                        files, this path should finish by "/",
    ///                        i.e. "/opt/ros/hydro/share/"
    /// \note the parser will replace "package://" by meshDataRootDir in the
    ///       urdf file.
    /// a groupNode is created and each link will be child node of this node.
    /// Links are named "prefix/linkName", linkName is the name of the link
    /// in the URDF file.
    ///
    /// Unlike addURDF, this method will load the collision geometry instead
    /// of the visual geometry.
    boolean addUrdfCollision (in string robotName, in string urdfFilePath,
			      in string meshDataRootDir) raises (Error);

    /// Create a node from an urdf file
    /// \param nodeName Name of the node that will contain the objects,
    ///        each object is prefixed by this name,
    /// \param urdfFilePath to the package containing the urdf file,
    ///        i.e. "/opt/ros/hydro/share/pr2_description",
    /// \param meshDataRootDir path to the package that contains the collada
    ///        files, this path should finish by "/", i.e.
    ///        "/opt/ros/hydro/share/"
    /// \note the parser will replace "package://" by meshDataRootDir in the
    ///       urdf file,
    /// \note visual whether to load collision or visual geometry.
    /// a groupNode is created and each object will be child node of this node.
    /// Objects are named "prefix/linkName", linkName is the name of the link
    /// in the URDF file. Unlike addURDF and addUrdfCollision, the node frame
    /// corresponds to the position of objects hold by the links and not to
    /// the position of objects.
    void addUrdfObjects (in string nodeName, in string urdfFilePath,
			 in string meshDataRootDir, in boolean visual)
      raises (Error);

    /// create a groupNode called groupName
    /// \param input groupName : name of the group.
    boolean createGroup(in string groupName) raises (Error);

    /// set a node as child to a group node.
    /// \param input nodeName : name of the child node.
    /// \param input groupName : name of the mother node (=group node).
    boolean addToGroup(in string nodeName, in string groupName) raises (Error);

    /// remove the node from the child list of a group node.
    /// \param input nodeName : name of the child node.
    /// \param input groupName : name of the mother node (=group node).
    boolean removeFromGroup(in string nodeName, in string groupName) raises (Error);

    /// Set a new configuration to a node. THIS CONFIGURATION WILL AFFECTIVE ONLY AFTER CALLING REFRESH FUNCTION!!!
    /// \param input nodeName : name of the node.
    /// \param input configuration : Float[7] new configuration.
    boolean applyConfiguration(in string nodeName, in Transform configuration) raises (Error);

    /// Add Landmark at the center of a node
    /// The colors are x=red, y=green, z=blue (xyz=RGB).
    /// \param input nodeName : name of the node
    /// \param input size : lenght of axises.
    boolean addLandmark(in string nodeName, in float size) raises (Error);

    /// delete Landmark of a node.
    /// \param input nodeName : name of the node
    boolean deleteLandmark(in string nodeName) raises (Error);

    /// Change the visibility of a node. If this node is a group node, visibility mode will be apply to all children.
    /// \param input nodeName : name of the node.
    /// \param input visibilityMode : visibility mode can be "ON", "OFF" or "ALWAYS_ON_TOP".
    boolean setVisibility(in string nodeName, in string visibilityMode) raises (Error);
    
       /// Change the scale of a node. 
    /// \param input nodeName : name of the node.
    /// \param input scale : new scale  
    boolean setScale(in string nodeName, in Position scale) raises (Error);


    /// Change wireframe mode of a node. If this node is a group node, wireframe mode will be apply to all children.
    /// \param input nodeName : name of the node.
    /// \param input wireframeMode : wiredframe mode can be "FILL", "WIREFRAME" or "FILL_AND_WIREFRAME".
    boolean setWireFrameMode(in string nodeName, in string wireFrameMode) raises (Error);

    /// Change lighting mode of a node. If this node is a group node, lighting mode will be apply to all children.
    /// \param input nodeName : name of the node.
    /// \param input lightingMode : lighting mode can be "ON" or "OFF".
    boolean setLightingMode(in string nodeName, in string lightingMode) raises (Error);

    /// Start capturing a window into image files.
    /// \param windowId the ID of the window
    /// \param filename, extension image files will be
    /// '<filename>_<sequence_number>.<extension>'
    ///
    /// \note To make a movie from the sequence of images, you can run:
    /// \code{.sh}
    ///   ffmpeg -r 50 -i capture_0_%d.jpeg -r 25 -vcodec libx264 video.mp4
    /// \endcode
    /// Note that <EM>libx264</EM> is not installed by default. On ubuntu 12.04,
    /// it is part of the package <EM>libavcodec-extra-53</EM>
    boolean startCapture (in unsigned long windowId, in string filename,
      in string extension) raises (Error);

    /// Stop the running capture of a window.
    boolean stopCapture (in unsigned long windowId) raises (Error);

    boolean writeNodeFile (in unsigned long windowId, in string filename) raises (Error);
  };
};
};
